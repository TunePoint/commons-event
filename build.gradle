import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.6.6' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'java'
    id 'maven-publish'
}

allprojects {
    group = 'ua.tunepoint'
    version = '0.0.6'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        maven {
            url 'http://artifactory.tunepoint.fun/releases'
            allowInsecureProtocol true
        }
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }

        dependencies {

            dependency 'org.springframework.boot:spring-boot-starter:2.6.6'
            dependency 'org.springframework.kafka:spring-kafka:2.8.4'
            dependency 'org.springframework.boot:spring-boot-configuration-processor:2.6.6'

            dependency 'com.fasterxml.jackson.core:jackson-core:2.13.2'
            dependency 'com.fasterxml.jackson.core:jackson-databind:2.13.2'
            dependency 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.2'
            dependency 'org.projectlombok:lombok:1.18.20'

            dependency 'org.springframework.boot:spring-boot-starter-test:2.6.6'
            dependency 'org.springframework.kafka:spring-kafka-test:2.8.4'
        }
    }

    if (project.file('publish.gradle').exists()) {

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId project.group
                    artifactId project.name
                    version project.version

                    artifact jar
                }
            }

            repositories {
                maven {
                    name  'reposilite'
                    url "http://${System.getenv('ARTIFACTORY_HOST')}/${System.getenv('ARTIFACTORY_REPOSITORY')}"
                    allowInsecureProtocol true

                    credentials {
                        username System.getenv('ARTIFACTORY_USERNAME')
                        password System.getenv('ARTIFACTORY_PASSWORD')
                    }
                }
            }
        }
    }
}
